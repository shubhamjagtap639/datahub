apply plugin: 'play'

// Change this to listen on a different port
project.ext.httpPort = 9001
project.ext.playBinaryBaseName = "wherehows-frontend"

tasks.withType(PlayRun) {
  httpPort = project.ext.httpPort
}

configurations {
  assets
}

dependencies {
  assets project(path: ':wherehows-web', configuration: 'assets')

  play project(":wherehows-dao")

  play externalDependency.play_java_ws
  play externalDependency.play_java_jdbc
  play externalDependency.play_filter
  play externalDependency.play_cache
  play externalDependency.mysql

  playTest externalDependency.play_test
  playTest externalDependency.festAssert
  playTest externalDependency.mockito

  playRun externalDependency.play_docs
}

model {
  components {
    play {
      platform play: '2.4.6', scala: '2.11', java: '1.8'

      binaries.all {
        tasks.withType(PlatformScalaCompile) {
          scalaCompileOptions.forkOptions.jvmArgs = ['-Xms1G', '-Xmx1G', '-Xss2M']
        }
      }

      binaries.all { binary ->
        binary.assets.addAssetDir unzipAssets.destinationDir
        binary.assets.builtBy unzipAssets
      }
    }
  }
  distributions {
    playBinary {
      baseName = project.ext.playBinaryBaseName
    }
  }
}

task unzipAssets(type: Copy, dependsOn: configurations.assets) {
  into "${buildDir}/assets"
  from {
    configurations.assets.collect { zipTree(it) }
  }
}
